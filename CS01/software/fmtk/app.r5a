; ============================================================================
;        __
;   \\__/ o\    (C) 2020  Robert Finch, Stratford
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

	code
	align	4

;------------------------------------------------------------------------------
; Parameters:
;   none
; Returns:
;   $a0 = app id (0 to 31), -1 if none available
;------------------------------------------------------------------------------
  align 16
AllocAppId:
  ldt   $a1,APPID_POOL
  ldi   $a0,#31
.0002:
  bltz  $a1,.0001
  ldi   $t0,#1
  sll   $t0,$t0,$a0
  or    $a1,$a1,$t0
  stt   $a1,APPID_POOL
  ret
.0001:
  sll   $a1,$a1,#1
  sub   $a0,$a0,#1
  bge   $a0,$x0,.0002
  ldi   $a0,#-1
  ret

;------------------------------------------------------------------------------
; Parameters:
;   $a1 = pointer to application start record
;------------------------------------------------------------------------------
  align 16
FMTK_StartApp:
  mov   $s1,$a1
  mov   $a0,$x0           ; OS owns the ACB
  ldi   $a1,#4096         ; size of application control block
  call  PAMAlloc          ; allocate ACB
  mov   $s3,$a1           ; $s3 = pointer to ACB
  ldi   $t0,#ACB_MAGIC
  stt   $t0,OBJMagic[$s3]
  stt   $x0,ACBGarbageList[$s3]
  ldb   $t0,ASRmid[$s1]
  stb   $t0,ACBmid[$s3]
  ; Allocate a virtual screen and set pointers to it.
  srl   $a0,$s3,#LOG_PGSZ ; App owns the screen
  ldi   $a1,#8192         ; size of virtual screen
  call  PAMAlloc
  bltz  $a0,.noMem
  stt   $a1,ACBpVidMem[$s3]
  stt   $a1,ACBpVirtVidMem[$s3]
  ; Setup screen attributes
  ldi   $t0,#56
  stb   $t0,ACBVideoCols[$s3]
  ldi   $t0,#31
  stb   $t0,ACBVideoRows[$s3]
  stb   $x0,ACBCursorRow[$s3]
  stb   $x0,ACBCursorCol[$s3]
  ldi   $t0,#$0087FC00
  stt   $t0,ACBNormAttr[$s3]

  ; Allocate memory for application code and copy code into position
  ldt   $a1,ASRCodeSize[$s1]
  beqz  $a1,.0004         ; 0 size code = in ROM
  call  Alloc
  bltz  $a0,.noMem
  stt   $a1,ACBpCode[$s3] ; save pointer to code area in ACB
  mov   $s4,$a1           ; $s4 = pointer to application code
  mov   $t0,ASRCodeSize[$s1]  ; $t0 = code size
  ldt   $s5,ASRpCode[$s1] ; $s5 = pointer to application code
.0001:
  ldt   $t1,[$s5]
  stt   $t1,[$s4]
  add   $s5,$s5,#4
  add   $s4,$s4,#4
  sub   $t0,$t0,#1
  bgtz  $t0,.0001
  bra   .0003
.0004:
  ldt   $s5,ASRpCode[$s1] ; $s5 = pointer to application code
  stt   $s5,ACBpCode[$s3] ; save pointer to code area in ACB

.0003:
  ; Allocate memory for application data and copy data into position
  ; The memory requirements for both initialized and uninitialized data are
  ; summed and a single block of memory allocated for both.
  ; Vars will then be accessed via a global pointer offset from the start
  ; of the data area.
  stt   $x0,ACBpData[$s3]
  stt   $x0,ACBpUIData[$s3]
  ldt   $a1,ASRDataSize[$s1]
  ldt   $a2,ASRIUDataSize[$s1]
  add   $a1,$a1,$a2
  beqz  $a1,.0005         ; no data?
  call  Alloc
  bltz  $a0,.noMem
  stt   $a1,ACBpData[$s3]

  ; Copy pre-initialized data to data area.
  mov   $s4,$a1           ; $s4 = pointer to application data
  ldt   $t0,ASRDataSize[$s1]  ; $t0 = data size
  add   $t0,$t0,#3        ; round to whole words
  and   $t0,$t0,#$FFFFFFFC
  ldt   $s5,ASRpData[$s1] ; $s5 = pointer to application data
.0002:
  ldt   $t1,[$s5]
  stt   $t1,[$s4]
  add   $s5,$s5,#4
  add   $s4,$s4,#4
  sub   $t0,$t0,#4
  bgtz  $t0,.0002

.0005:
  ; Record pointer to uninitialized data
  ldt   $a0,ASRDataSize[$s1]
  ldt   $a1,ACBpData[$s1]
  add   $a1,$a0,$a1
  add   $a1,$a1,#3
  and   $a1,$a1,#$FFFFFFFC
  stt   $a1,ACBpUIData[$s3]

.0006:
  ; Allocate memory for heap
  ldt   $a1,ASRHeapSize[$s1]
  beqz  $a1,.0007         ; no heap?
  call  Alloc
  bltz  $a0,.noMem
  stt   $a1,ACBHeap[$s3]

.0007:
  ldi   $a0,#1            ; IStartTask
  srl   $a1,$s3,#LOG_PGSZ ; $a1 = app id
  stt   $a1,$3F48
  stt   $s3,$3F50
  ldi   $a2,#1024         ; task local memory
  ldt   $a3,ASRpCode[$s1] ; get pointer to code area
  ldi   $a4,#$04
  call  OSOCall           ; start the initial app task
  bnez  $a0,.tskErr       ; error?
  stw   $a1,ACBTask[$s3]
  stw   $a1,$3F48
  sll   $t0,$a1,#LOG_PGSZ
  ldt   $t1,ACBpData[$s3]
  stt   $t1,TCBxGP[$t0]   ; set global pointer to data area
  ldi   $t1,#AS_LIVE
  stb   $t1,ACBStatus[$s3]
  srl   $a1,$s3,#LOG_PGSZ ; return app id
  ldi   $a0,#E_Ok
  jmp   OSExit
.tskErr:
  ldi   $t1,#AS_UNDEAD
  stb   $t1,ACBStatus[$s3]
  jmp   OSExit

.noMoreApps:
  ldi   $a0,#E_NoMoreAppIds
.noMem
  jmp   OSExit

;------------------------------------------------------------------------------
; Exit the current task.
;
; Parameters:
;		none
; Modifies:
;		a1 = task id
;------------------------------------------------------------------------------

FMTK_ExitApp:
	mGetCurrentTid
	sll		$a1,$a0,#LOG_TCBSZ  ; convert TID to TCB address
	ldb   $a1,TCBmid[$a1]     ; get the mid
	; fall through to KillApp
	
;------------------------------------------------------------------------------
; Parameters:
;		a1 = mid of task to kill
;------------------------------------------------------------------------------
  align 16
FMTK_KillApp:
	beqz	$a1,.immortal		    ; app #0 is immortal (the system)
	ldi		$t0,#TS_DEAD				; flag app as dead (prevents it from being re-queued)
	sll		$a1,$a1,#2	        
	ldt   $a1,TidTcbMap[$a1]  ; convert TID to TCB address
	stb		$t0,TCBStatus[$a1]
;	ldbu  $a0,TCBmid[$a1]
;	call	FreeAll							; free all the memory associated with the task
	; Now make task ID available for reuse
	ldbu  $a0,TCBtid[$a1]
.immortal:
	ldi		$a0,#E_Ok
	jmp		OSExit

