; ============================================================================
;        __
;   \\__/ o\    (C) 2020  Robert Finch, Stratford
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

MBX_OWNER			equ		0		; tid of owning task
MBX_WTIDS			equ		8		; tasks waiting at mailbox
MBX_MQHEAD		equ		16		
MBX_MQTAIL		equ		24
MBX_SIZE			equ		32
LOG_MBXSZ			equ		5

MSG_LINK	equ		0
MSG_D1		equ		8
MSG_D2		equ		16
MSG_D3		equ		24
MSG_SIZE	equ		32
LOG_MSGSZ	equ		5

;	bss
;	align 4
;FreeMsg	dw	0
;msgs:
;	fill.b	MSG_SIZE*1024,0
;mbxs:
;	fill.b	MBX_SIZE*32,0
;mbxs_end:

	code
	align	4

;------------------------------------------------------------------------------
; Remove the task from the list of tasks waiting at the mailbox.
; This routine is only called from a couple of places and it is convenient
; not to stack the return address. So, it is implemented as a macro.
;
; Parameters:
;		a0 = task id
;------------------------------------------------------------------------------

MbxRemoveTask:
	sub		$sp,$sp,#32
	sto		$s1,[$sp]
	sto		$t0,8[$sp]
	sto		$t1,16[$sp]
	sto		$t2,24[$sp]
	pfi
	sll		$s1,$a0,#LOG_TCBSZ		; tid to pointer
	ldw		$t0,TCBWaitMbx[$s1]		; get mailbox handle
	blt		$t0,$x0,.xit					; handle good?
	sll		$t0,$t0,#LOG_MBXSZ		; convert to pointer
	add		$t0,$t0,#mbxs					; by adding base address
	pfi
	ldo		$t1,MBX_WTIDS[$t0]		; get waiting task list
	ldi		$t2,#1								; create a mask for given task id
	sll		$t2,$t2,$a0
	xor		$t2,$t2,#-1
	and		$t1,$t1,$t2						; clear bit
	sto		$t1,MBX_WTIDS[$t0]		; update waiting task list
.xit:
	pfi
	ldo		$s1,[$sp]
	ldo		$t0,8[$sp]
	ldo		$t1,16[$sp]
	ldo		$t2,24[$sp]
	add		$sp,$sp,#32
	ret

;------------------------------------------------------------------------------
; Allocate a mailbox.
;
; Parameters:
;		a1 = task id of owner
;		a2 = pointer where to store handle
; Returns:
;		v0 = E_Ok
;------------------------------------------------------------------------------

FMTK_AllocMbx:
	sub		$sp,$sp,#8
	sto		$ra,[$sp]
	beq		$a2,$x0,.badArg
	ldi		$t0,#mbxs
	; Lock system mutex
	mGetCurrentTid
	mov		$s6,$v0
	sll		$v0,$v0,#1
	or		$v0,$v0,#1
	ldo		$v1,SYS_MUTEX
	beq		$v0,$v1,AlreadyLocked
.lock:
	pfi
	sto		$v0,SYS_MUTEX
	ldo		$v1,SYS_MUTEX
	bne		$v0,$v1,.lock
.nxt:
	pfi
	ldbu	$t1,MBX_OWNER[$t0]
	beq		$t1,$x0,.noOwner
	add		$t0,$t0,#MBX_SIZE
	slt		$t1,$t0,#mbxs_end
	bne		$t1,$x0,.nxt
	ldi		$v0,#E_NoMoreMbx
	bra		.xit
.noOwner:
	pfi
	stb		$a1,MBX_OWNER[$t0]
	sub		$t5,$t0,#mbxs				; convert pointer to handle
	srl		$t5,$t5,#LOG_MBXSZ
	mov		$a0,$a1
	mov		$a1,$a2
	pfi
	call	VirtToPhys
	sto		$t5,[$v0]
	ldi		$v0,#E_Ok
	bra		.xit
.badArg:
	ldi		$v0,#E_Arg
.xit:
	pfi
	; Unlock system mutex
	sll		$t0,$s6,#1
	sto		$t0,SYS_MUTEX
	sto		$t0,SYS_MUTEX
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret
AlreadyLocked:
	ldi		$v0,#E_Busy
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret

;------------------------------------------------------------------------------
; Free a mailbox
;
;------------------------------------------------------------------------------

FMTK_FreeMbx:
	sub		$sp,$sp,#8
	sto		$ra,[$sp]
	slt		$s1,$a1,#NR_MBX
	beq		$s1,$x0,.badMbx
	sll		$s1,$a1,#LOG_MBXSZ		; convert handle to pointer
	add		$s1,$s1,#mbxs
	ldi		$s2,#0
	ldi		$s4,#NR_TCB						; possibly 64 tasks
	; Lock system mutex
	mGetCurrentTid
	mov		$s7,$v0
	sll		$v0,$v0,#1
	or		$v0,$v0,#1
	ldo		$v1,SYS_MUTEX
	beq		$v0,$v1,AlreadyLocked
.lock:
	pfi
	sto		$v0,SYS_MUTEX
	ldo		$v1,SYS_MUTEX
	bne		$v0,$v1,.lock

	ldo		$s6,MBX_WTIDS[$s1]
.0002:
	pfi
	and		$s3,$s6,#1
	beq		$s3,$x0,.0001
	sll		$s5,$s2,#LOG_TCBSZ		; tid to pointer
	lbu		$v0,TCBStatus[$s5]
	and		$v0,$v0,#~TS_WAITMSG	; no longer waiting
	stb		$v0,TCBStatus[$s5]
	and		$v0,$v0,#TS_TIMEOUT
	mov		$a0,$s2
	beqz	$v0,.0003
	pfi
	call	RemoveFromTimeoutList
.0003:
	call	InsertIntoReadyList
	ldi		$v0,#E_NoMsg					; but no message
	sto		$v0,128[$s5]					; v0 = E_NoMsg
.0001:
	pfi
	srl		$s6,$s6,#1
	add		$s2,$s2,#1
	bltu	$s2,$s4,.0002
	ldi		$v0,#E_Ok
	bra		.xit
.badMbx:
	ldi		$v0,#E_BadMbx				; return null pointer if bad mailbox
.xit:
	pfi
	; Unlock system mutex
	sll		$t0,$s7,#1
	sto		$t0,SYS_MUTEX
	sto		$t0,SYS_MUTEX
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret

;------------------------------------------------------------------------------
; Send a message to a mailbox.
; The message will be broadcast to any waiting tasks. Waiting tasks will then
; be moved to the ready list. If there are no waiting tasks then the message
; is queued at the mailbox.
;
; Register Usage:
;		t0 = mailbox pointer
;		t1 = message pointer
;		s1 = task id of waiting task
; Modifies:
;		a0
; Parameters:
;		a1 = mailbox handle
;		a2 = message d1
;		a3 = message d2
;		a4 = message d3
;------------------------------------------------------------------------------

FMTK_SendMsg:
	sub		$sp,$sp,#8
	sto		$ra,[$sp]
	sltu	$t0,$a1,#NR_MBX
	beq		$t0,$x0,.badMbx
	sll		$t0,$a1,#LOG_MBXSZ	; convert handle to pointer
	add		$t0,$t0,#mbxs
	; Lock system mutex
	mGetCurrentTid
	mov		$s6,$v0
	sll		$v0,$v0,#1
	or		$v0,$v0,#1
	ldo		$v1,SYS_MUTEX
	beq		$v0,$v1,AlreadyLocked
.lock:
	pfi
	sto		$v0,SYS_MUTEX
	ldo		$v1,SYS_MUTEX
	bne		$v0,$v1,.lock

	ldo		$t5,MBX_WTIDS[$t0]
	beq		$t5,$x0,.noWaiters	; no waiters = go queue message
	ldi		$s1,#0
.0001:
	pfi
	and		$s3,$t5,#1					; is tid waiting?
	beq		$s3,$x0,.nxtTid
	sll		$s3,$s1,#LOG_TCBSZ	; convert tid to TCB pointer
	sto		$a2,TCBMsgD1[$s3]		; copy message to TCB
	sto		$a3,TCBMsgD2[$s3]
	sto		$a4,TCBMsgD3[$s3]
	pfi
	ldbu	$t2,TCBStatus[$s3]
	or		$t2,$t2,#TS_MSGRDY
	stb		$t2,TCBStatus[$s3]
	mov		$a0,$s1
	sub		$sp,$sp,#16
	sto		$ra,[$sp]
	sto		$t0,8[$sp]						; push t0
	pfi
	call	InsertIntoReadyList
	ldo		$ra,[$sp]
	ldo		$t0,8[$sp]						; pop t0
	add		$sp,$sp,#16
.nxtTid:
	pfi
	srl		$t5,$t5,#1					; check next task
	add		$s1,$s1,#1
	and		$s1,$s1,#63
	bne		$s1,$x0,.0001
	sto		$x0,MBX_WTIDS[$t0]	; clear waiters
	ldi		$v0,#E_Ok
	bra		.xit
.noWaiters:
	pfi
	ldo		$t1,FreeMsg
	beq		$t1,$x0,.noMsg			; message available?
	ldo		$t2,MSG_LINK[$t1]
	sto		$t2,FreeMsg
	sto		$a2,MSG_D1[$t1]
	sto		$a3,MSG_D2[$t1]
	sto		$a4,MSG_D3[$t1]
	pfi
	ldo		$t3,MBX_MQTAIL[$t0]
	beq		$t3,$x0,.mbxEmpty
	sto		$t1,MSG_LINK[$t3]
	sto		$t1,MBX_MQTAIL[$t0]
	ldi		$v0,#E_Ok
	bra		.xit
.mbxEmpty:
	pfi
	sto		$t1,MBX_MQHEAD[$t0]
	sto		$t1,MBX_MQTAIL[$t0]
	ldi		$v0,#E_Ok
	bra		.xit
.noMsg:
	pfi
	ldi		$v0,#E_NoMsg
	bra		.xit
.badMbx:
	ldi		$v0,#E_BadMbx				; return null pointer if bad mailbox
.xit:
	; Unlock system mutex
	sll		$t0,$s6,#1
	sto		$t0,SYS_MUTEX
	sto		$t0,SYS_MUTEX
	pfi
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret

;------------------------------------------------------------------------------
; PeekMsg will check for a message at a mailbox optionally dequeuing it.
; If no message is available PeekMsg returns to the caller with a E_NoMsg
; status.
;
; Parameters:
;		a1 = mailbox handle
;		a2 = pointer where to put message D1
;		a3 = pointer where to put message D2
;		a4 = pointer where to put message D3
;		a5 = 1 = remove from queue
;------------------------------------------------------------------------------

FMTK_PeekMsg:
	sub		$sp,$sp,#8
	sto		$ra,[$sp]
	slt		$t0,$a1,#32
	pfi
	beqz	$t0,.badMbx
	; Lock system mutex
	mGetCurrentTid
	mov		$s6,$v0
	sll		$v0,$v0,#1
	or		$v0,$v0,#1
	ldo		$v1,SYS_MUTEX
	beq		$v0,$v1,AlreadyLocked
.lock:
	pfi
	sto		$v0,SYS_MUTEX
	ldo		$v1,SYS_MUTEX
	bne		$v0,$v1,.lock

	sll		$t2,$a1,#LOG_MBXSZ	; convert handle to pointer
	add		$t2,$t2,#mbxs
	ldo		$t1,MBX_MQHEAD[$t2]
	beq		$t1,$x0,.noMsg
	beq		$a5,$x0,.nodq
	ldo		$t3,MSG_LINK[$t1]
	sto		$t3,MBX_MQHEAD[$t2]
	ldo		$t3,FreeMsg
	sto		$t3,MSG_LINK[$t1]
	sto		$t1,FreeMsg
.nodq:
	pfi
	ldi		$t0,#$10000
	beqz	$a2,.nod1
	ldo		$t3,MSG_D1[$t1]
	csrrs	$x0,#$300,$t0				; set mprv
	sto		$t3,[$a2]
	csrrc	$x0,#$300,$t0				; clear mprv
.nod1:
	pfi
	beqz	$a3,.nod2
	ldo		$t3,MSG_D2[$t1]
	csrrs	$x0,#$300,$t0				; set mprv
	sto		$t3,[$a3]
	csrrc	$x0,#$300,$t0				; clear mprv
.nod2:
	pfi
	beqz	$a4,.nod3
	ldo		$t3,MSG_D3[$t1]
	csrrs	$x0,#$300,$t0				; set mprv
	sto		$t3,[$a4]
	csrrc	$x0,#$300,$t0				; clear mprv
.nod3:
	pfi
	ldi		$v0,#E_Ok
	bra		.ret
.noMsg:
	pfi
	ldi		$v0,#E_NoMsg
	bra		.ret
.badMbx:
	ldi		$v0,#E_BadMbx				; return null pointer if bad mailbox
.ret:
	pfi
	; Unlock system mutex
	sll		$t0,$s6,#1
	sto		$t0,SYS_MUTEX
	sto		$t0,SYS_MUTEX
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret

;------------------------------------------------------------------------------
; Calling WaitMsg will cause the task to be queued at the mailbox and a task
; switch to occur if there are no messages at the mailbox.
;
; Parameters:
;		a1 = mailbox handle
;		a2 = pointer where to put message D1
;		a3 = pointer where to put message D2
;		a4 = pointer where to put message D3
;		a5 = time limit
;------------------------------------------------------------------------------

FMTK_WaitMsg:
	sub		$sp,$sp,#8
	sto		$ra,[$sp]
	mov		$s5,$a5
	ldi		$a5,#1
	pfi
	call	FMTK_PeekMsg				; check for a message, return if available
	ldi		$t1,#E_NoMsg
	beq		$v0,$t1,.qt					; no message? Then go queue task
	bra		.xit
.qt:
	pfi
	; Lock system mutex
	mGetCurrentTid
	mov		$s6,$v0
	sll		$v0,$v0,#1
	or		$v0,$v0,#1
	ldo		$v1,SYS_MUTEX
	beq		$v0,$v1,AlreadyLocked
.lock:
	pfi
	sto		$v0,SYS_MUTEX
	ldo		$v1,SYS_MUTEX
	bne		$v0,$v1,.lock

	ldi		$t2,#1
	sll		$t2,$t2,$v0
	sll		$t3,$a1,#LOG_MBXSZ	; convert handle to pointer
	add		$t3,$t3,#mbxs
	pfi
	ldo		$t4,MBX_WTIDS[$t3]	; get waiting task list
	or		$t4,$t4,$t2					; set bit for tid
	sto		$t4,MBX_WTIDS[$t3]	; save task list
	sll		$t4,$v0,#LOG_TCBSZ	; convert tid to TCB pointer
	ldbu	$t3,TCBStatus[$t4]
	or		$t3,$t3,#TS_WAITMSG	; set waiting for message status
	and		$t3,$t3,#~TS_READY	; not ready
	pfi
	stb		$t3,TCBStatus[$t4]
	stb		$a1,TCBWaitMbx[$t4]	; set mailbox task is waiting for
	mov		$a1,$a5
	; Unlock system mutex
	sll		$t0,$s6,#1
	sto		$t0,SYS_MUTEX
	sto		$t0,SYS_MUTEX
	; Continue by switching tasks
	call		FMTK_Sleep
.xit:
	pfi
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret

	