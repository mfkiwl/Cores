; ============================================================================
;        __
;   \\__/ o\    (C) 2020  Robert Finch, Stratford
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

	code
	align	4
;------------------------------------------------------------------------------
; Insert task into ready queue.
;
; Parameters:
;		$a0 = tid to insert
; Modifies:
;	  $t0,$t1
; Returns:
;		none
;------------------------------------------------------------------------------

InsertIntoReadyQueue:
	sll   $t0,$a0,#2
	ldt   $t0,TidTcbMap[$t0]
	ldbu	$t1,TCBStatus[$t0]	; set status to ready
	or		$t1,$t1,#TS_READY
	stb		$t1,TCBStatus[$t0]
	ldb		$t1,TCBPriority[$t0]
	pushq	$a0,$t1
	ret

;------------------------------------------------------------------------------
; Removal from the ready queue is easy. Just flag the task status as no longer
; ready or running. The next time it appears in a dequeue the code will ignore
; it and pop the next ready task.
;
; Parameters:
;		a0 = tid to remove
; Modifies:
;		t0,t1
; Returns:
;		v0 = E_Ok for success
;------------------------------------------------------------------------------

RemoveFromReadyQueue:
	sll   $t0,$a0,#2
	ldt   $t0,TidTcbMap[$t0]
	ldb		$t1,TCBStatus[$t0]		; set status no longer ready or running
	and		$t1,$t1,#~(TS_READY|TS_RUNNING)
	stb		$t1,TCBStatus[$t0]
	ret

;------------------------------------------------------------------------------
; Parameters:
;		a0 = task id to insert
;		a1 = timeout value
; Modifies:
;		t0,t1,t2,t3,t4,t5
;------------------------------------------------------------------------------

InsertIntoTimeoutList:
	setto	$a0,$a1
	sll   $t5,$a0,#2
	ldt   $t5,TidTcbMap[$t5]
	ldbu	$t2,TCBStatus[$t5]		; set status to timing out
	or		$t2,$t2,#TS_TIMEOUT
	stb		$t2,TCBStatus[$t5]
	ldi		$v0,#E_Ok
	ret

;InsertIntoTimeoutList:
  sll   $a0,$a0,#LOG_TCBSZ
	stt		$x0,TCBNext[$a0]			; initialize indexes to nullptr
	stt		$x0,TCBPrev[$a0]
	ldt		$t0,TimeoutList
	bnez	$t0,.0001
	; Here the timeout list was empty, easy to insert.
	stt		$a1,TCBTimeout[$a0]		; set timeout for task
	stt		$a0,TimeoutList				; set head of list
	srl   $a0,$a0,#LOG_TCBSZ
	ldi		$v0,#E_Ok							; and return
	ret
.0001:
	ldi		$t1,#0								; t1 used as previous index
	mov		$t3,$t0								; t3 = head of timeout list pointer
;	sll		$t3,$t2,#LOG_TCBSZ		; t3 = head of timeout list as a pointer
	; Search the timeout list for the proper insertion point. The timeouts are
	; relative, so the requested amount is decremented by the amounts in the
	; list as it is searched.
.beginWhile:
	ldt		$t4,TCBTimeout[$t3]		; get the timeout
	ble		$a1,$t4,.endWhile			; is it greater than the requested timeout ?
	sub		$a1,$a1,$t4						; decrement by list amount
	mov		$t1,$t3								; t1 = pointer to previous entry
	ldt		$t3,TCBNext[$t3]			; get the next entry in list
	beqz	$t3,.endOfList				; end of list?
;	sll		$t3,$t3,#LOG_TCBSZ		; no, set pointer to current entry
	bne		$t3,$t1,.beginWhile		; list screwed up? (This test should always be true)
.endOfList:
	; Here the end of the list was reached, insert as last entry
	stt		$a0,TCBNext[$t1]			; entry->next = arg
	stt		$t1,TCBPrev[$a0]			; arg->prev = entry
	stt		$a1,TCBTimeout[$a0]
	bra		.0003
.endWhile:
	; Here we found a timeout greater than the requested amount. Insert before.
;	sra		$t2,$t3,#LOG_TCBSZ		; pointer to tid
	stt		$t3,TCBNext[$a0]			; arg->next = entry
;	sra		$t2,$t1,#LOG_TCBSZ
	stt		$t1,TCBPrev[$a0]			; arg->prev = prev entry
	ldt		$t2,TCBTimeout[$t3]		; update timeout of entry by subtracting off
	sub		$t2,$t2,$a1						; the arg's timeout
	stt		$t2,TCBTimeout[$t3]
	stt		$a0,TCBPrev[$t3]			; entry->prev = arg
	beqz	$t1,.0002							; previous entry? no - go update list head
	stt		$a0,TCBNext[$t1]			; prev entry->next = arg
	bra		.0003
.0002:
	stt		$a0,TimeoutList				; list head = arg
.0003:
	ldbu	$t2,TCBStatus[$a0]		; set status to timing out
	or		$t2,$t2,#TS_TIMEOUT
	stb		$t2,TCBStatus[$a0]
	srl   $a0,$a0,#LOG_TCBSZ
	ldi		$v0,#E_Ok
	ret

;------------------------------------------------------------------------------
; Remove a task from the timeout list. The timeouts of following tasks are
; adjusted. Called when a mailbox is freed and a task is waiting at the
; mailbox. From within scheduling code mPopTimeoutList is used instead.
;
; Parameters:
;		a0 = task id to remove
; Modifies:
;		t0,t1,t2,t3,t4
; Returns:
;		none
;------------------------------------------------------------------------------

RemoveFromTimeoutList:
	setto	$a0,$x0
	ret
;RemoveFromTimeoutList:
	sll		$a0,$a0,#LOG_TCBSZ		; tid to pointer
	ldbu	$t0,TCBStatus[$a0]		; check if waiting at a mailbox
	and		$t0,$t0,#TS_WAITMSG
	beqz	$t0,.noWait				
	mMbxRemoveTask
.noWait:
	ldt		$t1,TimeoutList
	ldt 	$t0,TCBNext[$a0]			; t0 = arg->next
	bne		$a0,$t1,.0001					; check removing head of list
	stt		$t0,TimeoutList				; set new head = arg->next
.0001:
	beqz	$t0,.noNext
;	sll		$t4,$t0,#LOG_TCBSZ
	ldt		$t1,TCBPrev[$a0]			; next->prev = arg->prev
	stt		$t1,TCBPrev[$t0]			
	ldt		$t1,TCBTimeout[$t0]		; next->timeout += arg->timeout
	ldt		$t2,TCBTimeout[$a0]
	add		$t1,$t1,$t2
	stt		$t1,TCBTimeout[$t0]
.noNext:
	ldt		$t0,TCBPrev[$a0]			; check for previous
	beqz	$t0,.noPrev
	ldt		$t1,TCBNext[$a0]			; prev->next = arg->next
	stt		$t1,TCBNext[$t0]
.noPrev:
	ldb		$t0,TCBStatus[$a0]		; no longer timing out
	and		$t0,$t0,#~(TS_TIMEOUT|TS_WAITMSG)
	stb		$t0,TCBStatus[$a0]
	stt		$x0,TCBNext[$a0]      ; reset links
	stt		$x0,TCBPrev[$a0]
	srl   $a0,$a0,#LOG_TCBSZ
	ret

;------------------------------------------------------------------------------
; Parameters:
;   $a0 = app id
; Returns:
;		$v1 = task id (pointer to task control block)
;------------------------------------------------------------------------------

;AllocTCB:
  gcsub $sp,$sp,#4
  stt   $ra,[$sp]
  ldi   $a1,#1024
  call  Alloc
  ldt   $ra,[$sp]
  add   $sp,$sp,#4
  ret

AllocTCB:
	ldi		$t1,#0
	ldt	  $v1,PIDMAP
.0001:
	and		$t0,$v1,#1
	beqz	$t0,.allocTid
	srl		$v1,$v1,#1
	or		$v1,$v1,#$80000000
	add		$t1,$t1,#1
	and		$t1,$t1,#31
	bne		$t1,$x0,.0001
; here no tcbs available
	ldi		$v0,#E_NoMoreTCBs
	ret
.allocTid:
	mov		$v0,$t1
	or		$v1,$v1,#1
	beqz	$t1,.0003
.0002:
	sll		$v1,$v1,#1
	or		$v1,$v1,#1
	sub		$t1,$t1,#1
	bnez	$t1,.0002
.0003:
	stt		$v1,PIDMAP
	mov		$v1,$v0
	ldi		$v0,#E_Ok
	ret

;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

FreeTCB:
	ldi		$t0,#1
	sll		$t0,$t0,$a0
	xor		$t0,$t0,#-1
	ldt		$t1,PIDMAP
	and		$t1,$t1,$t0
	stt		$t1,PIDMAP
	ret

;------------------------------------------------------------------------------
; Diagnostics
;------------------------------------------------------------------------------

DumpReadyList:
	sub		$sp,$sp,#32
	stt		$ra,[$sp]
	stt		$a0,4[$sp]
	stt		$a2,8[$sp]
	stt		$a3,12[$sp]
	stt		$t1,16[$sp]
	stt		$t2,20[$sp]
	stt		$t3,24[$sp]
	stt		$s1,28[$sp]
	ldi		$a0,#msgReadyList
	call	PutString
	ldi		$t1,#0
	csrrc	$s1,#$300,#1
.0002:
;	call	SerialPeekCharDirect
;	xor		$v0,$v0,#CTRLC
;	beqz	$v0,.brk
	ldi		$a0,#CR
	call	Putch
	ldi		$a0,#'Q'
	call	Putch
	;srl		$a0,$t1,#1
	mov   $a0,$t1
	call	PutHexNybble
	ldi		$a0,#':'
	call	Putch
	ldi   $t2,#15
.0003:
;	getrdy  $a2,$x0,$t1
	popq  $a2,$t1
	pushq $a2,$t1
;	ldw		$a2,READYQ[$t1]
;	blt		$a2,$x0,.nxt
	mov		$a3,$a2
.0001:
	mov		$a0,$a3
	call	PutHexByte
	ldi		$a0,#' '
	call	Putch
	sub   $t2,$t2,#1
	bgtz  $t2,.0003
;	sll		$a3,$a3,#LOG_TCBSZ
;	ldw		$a0,TCBNext[$a3]
;	call	PutHexHalf
;	ldi		$a0,#' '
;	call	Putch
;	ldw		$a0,TCBPrev[$a3]
;	call	PutHexHalf
	ldi		$a0,#CR
	call	Putch
;	ldw		$a3,TCBNext[$a3]
;	bne		$a2,$a3,.0001
.nxt:
	add		$t1,$t1,#1
	slt		$t2,$t1,#4
	bnez	$t2,.0002
.brk:
	csrrw	$x0,#$300,$s1
	ldt		$ra,[$sp]
	ldt		$a0,4[$sp]
	ldt		$a2,8[$sp]
	ldt		$a3,12[$sp]
	ldt		$t1,16[$sp]
	ldt		$t2,20[$sp]
	ldt		$t3,24[$sp]
	ldt		$s1,28[$sp]
	add		$sp,$sp,#32
	ret

DumpTimeoutList:
	sub		$sp,$sp,#32
	stt		$ra,[$sp]
	stt		$a0,4[$sp]
	stt		$a2,8[$sp]
	stt		$a3,12[$sp]
	stt		$t1,16[$sp]
	stt		$t2,20[$sp]
	stt		$t3,24[$sp]
	stt		$s1,28[$sp]
	ldi		$a0,#msgTimeoutList
	call	PutString
	ldi		$t1,#0
	csrrc	$s1,#$300,#1
	ldi		$a3,#0
.0002:
	call	SerialPeekCharDirect
	xor		$v0,$v0,#CTRLC
	beqz	$v0,.brk
	ldi		$a0,#CR
	call	Putch
;	ldw		$a2,TimeoutList
;	bltz	$a2,.brk
;	mov		$a3,$a2
;.0001:
	mov		$a0,$a3
	call	PutHexHalf
	ldi		$a0,#'-'
	call	Putch
	getto	$a0,$t1
;	sll		$a3,$a3,#LOG_TCBSZ
;	ldt		$a0,TCBTimeout[$a3]
	call	PutHexWord
;	ldi		$a0,#CR
;	call	Putch
	add		$a3,$a3,#1
	and		$a3,$a3,#15
	bgt		$a3,$x0,.0002
;	ldw		$a3,TCBNext[$a3]
;	bge		$a3,$x0,.0001
.brk:
	csrrw	$x0,#$300,$s1
	ldt		$ra,[$sp]
	ldt		$a0,4[$sp]
	ldt		$a2,8[$sp]
	ldt		$a3,12[$sp]
	ldt		$t1,16[$sp]
	ldt		$t2,20[$sp]
	ldt		$t3,24[$sp]
	ldt		$s1,28[$sp]
	add		$sp,$sp,#32
	ret

msgReadyList:
	db	CR,"Ready List",CR
	db	"Que Tid  Prv  Nxt",CR
	db	"-----------------",CR,0

msgTimeoutList:
	db	CR,"Timeout List",CR
	db	" Tid - Timeout",CR
	db	"--------------",CR,0

	align 4